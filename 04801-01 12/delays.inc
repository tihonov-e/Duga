;RSEG PROG




          ;********************** 11.12.2006 ****************************************************
         ;====== Библиотека временных задержек под частоту кварца 22,11840 МГц. ===============
        ;====== ************************************************************** ===============
       ;====== Временная задержка 8.68 мкс.(начало) + 8.68 мкс.(конец)+ (1 * <DPTR>) сек. ===
      ;======                   (16 циклов с"lcall"+ 16 циклов с "ret"                   ===
     ;====== Входной параметр: в <DPTR> - кратен целому числу секунд задержки, ============
    ;====== и должен находиться в пределах от 0001H до FFFFH.                 ============
   ;-------------------------------------------------------------------------------------
delay_n_sek:      ; === Вызов пп. (lcall delay_n_sek) - 2 цикла.

                ; Получение в <DPTR> дополнительного кода от параметра целого числа секунд
               ; задержки. --------------------------------------------------------------
                mov     a,dpl     ; 1 цикл.
                cpl     a        ; 1 цикл.
                mov     dpl,a   ; 1 цикл.
                ;-------------
                mov     a,dph     ; 1 цикл.
                cpl     a        ; 1 цикл.
                mov     dph,a   ; 1 цикл.
                ;-------------
                inc     dptr   ; 2 цикла.

        ; Дополнительный код в <DPTR> от параметра целого числа секунд - получен. -------
                mov     zp_st,dph  ; 2 цикла.
                mov     zp_ml,dpl  ; 2 цикла.
               ;-----------------------------------------------------------------------------
cukl_delay_0:
                mov     dptr,#65536-10018 ; 2 цикла.
            ;-------------------------------------------------------------------------------
           ; До данного места пп. ^, с момента вызова (lcall delay_n_sek), проходит 16 циклов
          ; или 16х 0.5425 мкс. = 8.68 мкс.
         ;-------------------------------------------------------------------------------


      ; ---- Начало отрабатывания задержки 1 сек. ======================================
     ;---------------------------------------------------------------------------------
cukl_delay_0_a:
                mov     a,#88       ; 1 цикл.      !!! Начало отсчёта зад. =1 сек.
                nop                ; 1 цикл.
                ;----------------------------------------------------------------------------
cukl_delay_1:                                 ;<----
                                             ;     \
                djnz    acc,$     ; 2 цикла.       88 *2= 176 циклов внутри метки cukl_delay_1.
                                            ;      \     176 х 0.5425 мкс. = 95.48 мкс.
                                           ;   <----
                ;--------------------------    <------------------------------------------
                inc     dptr      ; 2 цикла.       \
                ;--                                \
                mov     a,dpl     ; 1 цикл.        6 циклов. = 6 х 0.5425 мкс. = 3.255 мкс.
                orl     a,dph     ; 1 цикл.        \
                jnz     cukl_delay_0_a; 2 цикла.   \
                ;------------------------      <-------------------------------------------
               ; Всего, с метки cukl_delay_0_a: 2+ 176+6 циклов = 184 цикла =
              ; 1.085 мкс.+ 95.48 мкс.+3.255 мкс.= 99.82 мкс. -------
             ;---------------------------------------------------------------------------

    ; ---- К данному моменту:
   ; ---- Фактическое время задержки, начиная с метки cukl_delay_0_a, составит:
  ; ---- 10018 х 99.82 мкс.= 999996.76 мкс.= 0.99999676 сек.             -----------------------
 ;---------------------------------------------------------------------------------------------
; ---- Конец отрабатывания задержки 1 сек. ===================================================


                mov     dph,zp_st ; 2 цикла. <------
                mov     dpl,zp_ml ; 2 цикла.       \
                ;----                              \
                inc     dptr      ; 2 цикла.       \
                ;---                               \
                mov     zp_st,dph ; 2 цикла.       \
                mov     zp_ml,dpl ; 2 цикла.       16 циклов
                ;-                                 \
                mov     a,dpl     ; 1 цикл.        \
                orl     a,dph     ; 1 цикл.        \
                jnz     cukl_delay_0; 2 цикла.->Переход на отрабатывание нового интервала 1 сек.
;--------------------------------------------;     \
                                            ;      \
                ret     ; 2 цикла.         ; <-----
;------------------------------------------




                ; ================== Временная задержка 0.1 сек. ================================
delay_01_sek:
                ; Загрузить счётчик формирования времени 0.1 сек.
                mov     dptr,#65536-205
                mov     zp_st,dph
                mov     zp_ml,dpl
                              ; Модуль счёта до переполнения счётчика - 205 полупериодов
                             ; частоты 1024 Гц. = 205 * 488.25 мкс. = 100091.25 мкс.=
                            ; 100.09125 мс.- определяет время задержки 0.1 сек.
                ;------------------------------------------------------------------------------

        ; Отрабатывание задержки 488.25 мкс.
       ; Задержка равна = T/2 (f=1024 Гц) =976.5625 мкс./2 = 488.28125 мкс.расчётн. или:
      ;  T/2 фактич.=900 машинных циклов = 900*0.5425 = 488.25 мкс.фактич.
     ;-------------------------------------------------------------------
cukl_d_ja_0:
                mov     dptr,#65536-147 ;                                      3 цикла  <---\
                                    ; 2 цикла.!!! Начало отсчёта задержки =900 циклов.      \
                nop                ; 1 цикл.                                            <---\
                ;------------------------------------------------------------------------
cukl_d_ja_1:                                  ;<----
                inc     dptr      ; 2 цикла.       \
                ;--                                \
                mov     a,dpl     ; 1 цикл.        147 *6= 882 цикла внутри метки cukl_d_ja_1.
                orl     a,dph     ; 1 цикл.        \          (+ 18 циклов вне данной метки)
                jnz     cukl_d_ja_1; 2 цикла.  <----
                ;---------------------------------------------------------------------------
                mov     dph,zp_st ; 2 цикла. <------
                mov     dpl,zp_ml ; 2 цикла.       \
                ;----                              \
                inc     dptr      ; 2 цикла.       \
                ;---                               15 циклов
                mov     zp_st,dph ; 2 цикла.       \
                mov     zp_ml,dpl ; 2 цикла.       \
                ;-                                 \
                nop               ; 1 цикл.        \
                mov     a,dpl     ; 1 цикл.        \
                orl     a,dph     ; 1 цикл.        \
                jnz     cukl_d_ja_0; 2 цикла. <-----
                ;------------------
                ;----------------------------------------------------------------------------
               ; Параметр модуля счёта в счётчике zp_st,z_ml =205,
              ; - он определяет общее время задержки:
             ; 205 * 488.25 мкс.(tзад.полупериода) = 100091.25 мкс.= 100.09125 мс.
            ;------------------------------------------------------------------------------
                ret



                ; ================== Временная задержка (<R7> x 0.1 сек.) ===============================
               ; ================== От (1х0,1)=0,1 сек. до (255х0,1)=25,5 сек. ========================
              ; ====== Входной параметр: в <R7> - кратен (целое число секунд задержки 1...255 X0,1 сек.)
             ;---------------------------------------------------------------------------------------
delay_n_x_01_sek:
                lcall   delay_01_sek
                djnz    R7,delay_n_x_01_sek
                ;------------------------------------------------------------------------------
                ret




;-----------------------------------------------------------------------------
                ; Задержка 10 мкс. (точнее: 9,765 мкс. Всего 18 циклов в пп.)
delay_10_mks:                     ; Вызов - 2 цикла.

                push    acc     ; 2 цикла.

                mov     a,#4  ; 1 цикл.
                nop          ; 1 цикл.

delay_10_mks_cukl:
                djnz    acc,delay_10_mks_cukl ; 2x4= 8 циклов.

                pop     acc ; 2 цикла.
                ret        ; 2 цикла.
;-------------------------------------------------------------------------


;-----------------------------------------------------------------------------
                ; Задержка 50 мкс. (точнее: 49,91 мкс. Всего 92 цикла в пп.)
delay_50_mks:                     ; Вызов - 2 цикла.

                push    acc     ; 2 цикла.

                mov     a,#41 ; 1 цикл.
                nop          ; 1 цикл.

delay_50_mks_cukl:
                djnz    acc,delay_50_mks_cukl ; 2x41= 82 цикла.

                pop     acc ; 2 цикла.
                ret        ; 2 цикла.
;-------------------------------------------------------------------------



                ; Задержка 100 мкс. (точнее: 99,82 мкс. Всего 184 цикла в пп.)
delay_100_mks:                     ; Вызов - 2 цикла.

                push    acc     ; 2 цикла.

                mov     a,#87 ; 1 цикл.
                nop          ; 1 цикл.

delay_100_mks_cukl:
                djnz    acc,delay_100_mks_cukl ; 2x87= 174 цикла.

                pop     acc ; 2 цикла.
                ret        ; 2 цикла.
;-------------------------------------------------------------------------



                ; Задержка 200 мкс. (точнее: 200,1825 мкс. Всего 369 циклов в пп.)
delay_200_mks:                     ; Вызов - 2 цикла.

                push    acc     ; 2 цикла.

                mov     a,#179; 1 цикла.
                nop          ; 1 цикл.
                nop         ; 1 цикл.

delay_200_mks_cukl:
                djnz    acc,delay_200_mks_cukl ; 2x179= 358 циклов.

                pop     acc ; 2 цикла.
                ret        ; 2 цикла.
;-------------------------------------------------------------------------




                ; Задержка 500 мкс. (точнее: 500,185 мкс. Всего 922 цикла в пп.)
delay_500_mks:                     ; Вызов - 2 цикла.

                push    acc     ; 2 цикла.

                mov     a,#227; 1 цикл.
                nop          ; 1 цикл.
                ;----------------------

delay_500_mks_cukl_1:
                djnz    acc,delay_500_mks_cukl_1 ; 2x227= 454 цикла.


                mov     a,#228; 2 цикла.

delay_500_mks_cukl_2:
                djnz    acc,delay_500_mks_cukl_2 ; 2x227= 456 циклов.

                pop     acc ; 2 цикла.
                ret        ; 2 цикла.
;-------------------------------------------------------------------------




                ; Задержка 1000 мкс. 1 мс. (точнее: 999,8275 мкс. Всего 1843 цикла в пп.)
delay_1_ms:                     ; Вызов - 2 цикла.

                push    acc     ; 2 цикла.

                mov     a,#227 ; 1 цикл.
                ;----------------------

delay_1_ms_cukl_1:
                djnz    acc,delay_1_ms_cukl_1 ; 2x227= 454 цикла.


                mov     a,#229; 1 цикл.
                nop          ; 1 цикл.
                ;----------------------
delay_1_ms_cukl_2:
                djnz    acc,delay_1_ms_cukl_2 ; 2x229= 458 циклов.


                mov     a,#229; 1 цикл.
                nop          ; 1 цикл.
                ;----------------------
delay_1_ms_cukl_3:
                djnz    acc,delay_1_ms_cukl_3 ; 2x229= 458 циклов.


                mov     a,#229; 1 цикл.
                nop          ; 1 цикл.
                ;----------------------
delay_1_ms_cukl_4:
                djnz    acc,delay_1_ms_cukl_4 ; 2x229= 458 циклов.

                pop     acc ; 2 цикла.
                ret        ; 2 цикла.
;-------------------------------------------------------------------------



                ; ================== Временная задержка (<R7> x 1 мс.) ===============================
               ; ================== От (1х1)=1 мс. до (255х1)=255 мс. ========================
              ; ====== Входной параметр: в <R7> - кратен (целому числу мсек. задержки 1...255 X1 мс.)
             ;---------------------------------------------------------------------------------------
delay_n_x_1_ms:
                lcall   delay_1_ms
                djnz    R7,delay_n_x_1_ms
                ;------------------------------------------------------------------------------
                ret

;_______________________________
DECDPTR:	PUSH	ACC	; Декрементирование DPTR на 1.

		MOV	A,DPL
		JB	ACC.7,DR_A

		DEC	DPL	; DPTR:=DPTR-1. (Если до декрементир. DPL.7:=0)
		MOV	A,DPL
		JB	ACC.7,DR_B ; Если есть заем в старший байт DPTR.

		POP	ACC
		RET

DR_A:		DEC	DPL	; DPTR:=DPTR-1. (Если до декрементир. DPL.7:=1)
		POP	ACC
		RET

DR_B:		DEC	DPH
		POP	ACC
		RET
;-------------------------------------------------------
















            ; ==== Временная задержка 0.1 сек. с анализом клавиш "Сброс/Стоп", "Вкл", Выкл", и выходом  =====
           ; ==== С УСТАНОВКОЙ ПЕРЕМЕННОЙ RES, POW_ON, POW_OFF В 0, в случае если предварительно была     =====
          ; ===  нажата соответствующаяя кнопка. ============================================
delay_01_sek_ESC:
                ;********************************************************************************
				SETB  RES ;УСТАНОВКА ФЛАГА КНОПКИ "СБРОС" В 1 (НЕ НАЖАТА)
            	SETB POW_ON
				SETB POW_OFF
				
				CLR	OE_BTN1	; ВКЛЮЧИМ БУФЕР КНОПОК
                ; Загрузить счётчик формирования времени 0.1 сек.
                mov     dptr,#65536-205
                mov     zp_st,dph
                mov     zp_ml,dpl
                              ; Модуль счёта до переполнения счётчика - 205 полупериодов
                             ; частоты 1024 Гц. = 205 * 488.25 мкс. = 100091.25 мкс.=
                            ; 100.09125 мс.- определяет время задержки 0.1 сек.
                ;------------------------------------------------------------------------------

        ; Отрабатывание задержки 488.25 мкс.
       ; Задержка равна = T/2 (f=1024 Гц) =976.5625 мкс./2 = 488.28125 мкс.расчётн. или:
      ;  T/2 фактич.=900 машинных циклов = 900*0.5425 = 488.25 мкс.фактич.
     ;-------------------------------------------------------------------
cukl_d_ja_0_ESC:
                mov     dptr,#65536-147 ;                                      2 цикла  <---\
                                    ; 2 цикла.!!! Начало отсчёта задержки =900 циклов.      \
                                   ;             (900х 0,5425= 488,25 мкс.)             <---\
                ;----------------------------------------------------------------------------
cukl_d_ja_1_ESC:                                ;<--
                inc     dptr      ; 2 цикла.       \
                ;--                                \
                mov     a,dpl     ; 1 цикл.        147 *6= 882 цикла внутри метки cukl_d_ja_1_ESC.
                orl     a,dph     ; 1 цикл.        \          (+ 18 циклов вне данной метки)
                jnz     cukl_d_ja_1_ESC; 2 цикла.<--
                ;---------------------------------------------------------------------------
                mov     dph,zp_st ; 2 цикла. <------
                mov     dpl,zp_ml ; 2 цикла.       \
                ;----                              \
                inc     dptr      ; 2 цикла.       \
                ;---                               16 циклов
                mov     zp_st,dph ; 2 цикла.       \
                mov     zp_ml,dpl ; 2 цикла.       \
                ;-                                 \
				jnb      PORT_RES,Exit_from_Mode_RES;2цикла\
				jnb      PORT_POW_ON,Exit_from_Mode_POW_ON;2цикла\
				jnb      PORT_POW_OFF,Exit_from_Mode_POW_OFF;2цикла\
				;----------------------------------\
                mov     a,dpl     ; 1 цикл.        \
                orl     a,dph     ; 1 цикл.        \
                jnz     cukl_d_ja_0_ESC; 2 цикла.<--
                ;------------------
                ;----------------------------------------------------------------------------
               ; Параметр модуля счёта в счётчике zp_st,z_ml =205,
              ; - он определяет общее время задержки:
             ; 205 * 488.25 мкс.(tзад.полупериода) = 100091.25 мкс.= 100.09125 мс.
            ;------------------------------------------------------------------------------
				ret

         ;****************************************************************************************
        ; Замена адреса возврата из пп. в стеке на вектор адреса метки <Cont_post_set_default:> -
       ; - кнопка была нажата. ************************************************

Exit_from_Mode_NO_TTI_S_1:
Exit_from_Mode_RES:
				jb      PORT_RES,Exit_from_Mode_with_RES
				;----------------------------------------------
                ;-------------
                jnb     PORT_RES,$
                ;-------------
			   
			    mov     R7,#5
                lcall   delay_n_x_1_ms ; Задержка 5 мс.
                ljmp    Exit_from_Mode_RES

Exit_from_Mode_POW_ON:
				jb      PORT_POW_ON,Exit_from_Mode_with_POW_ON
				;----------------------------------------------
                ;-------------
                jnb     PORT_POW_ON,$
                ;-------------
			   
			    mov     R7,#5
                lcall   delay_n_x_1_ms ; Задержка 5 мс.
                ljmp    Exit_from_Mode_POW_ON
                ;----------------------------------
                ;*********************************
Exit_from_Mode_POW_OFF:
				jb      PORT_POW_OFF,Exit_from_Mode_with_POW_OFF
				;----------------------------------------------
                ;-------------
                jnb     PORT_POW_OFF,$
                ;-------------
			   
			    mov     R7,#5
                lcall   delay_n_x_1_ms ; Задержка 5 мс.
                ljmp    Exit_from_Mode_POW_OFF
                ;----------------------------------
                ;*********************************



Exit_from_Mode_with_RES:
				CLR RES ; УСТАНОВКА В 0 ФЛАГА КНОПКИ "СБРОС" (НАЖАТА)
				JMP NEXT_EXIT_FROM_MODE	; ДАЛЬШЕ К ЗАМЕНЕ АДРЕСА ВЫХОДА 
Exit_from_Mode_with_POW_ON:
				CLR POW_ON ; УСТАНОВКА В 0 ФЛАГА КНОПКИ "Вкл" (НАЖАТА)
				JMP NEXT_EXIT_FROM_MODE	; ДАЛЬШЕ К ЗАМЕНЕ АДРЕСА ВЫХОДА
Exit_from_Mode_with_POW_OFF:
				CLR POW_OFF ; УСТАНОВКА В 0 ФЛАГА КНОПКИ "Выкл" (НАЖАТА)



NEXT_EXIT_FROM_MODE:
				SETB	OE_BTN1	 ;ВЫКЛЮЧИМ БУФЕР КНОПОК
                ;================
                ;**************;
                ; ЗАМЕНА СТЕКА И ДОСРОЧНЫЙ ВЫХОД
				MOV     DPTR,#EXIT_delay_n_x_01_sek_ESC_WITH_RES
                ;=================================
                MOV     A,SP
                MOV     R0,A

		        MOV	@R0,DPH
		        DEC	R0
		        MOV	@R0,DPL
                ;================
                ret
                ;nnnnnnnnnnnnnn
               ;*********************************************************



                ; ================== Временная задержка (<R7> x 0.1 сек.) =================================
               ; ================== с анализом клавиши "Сброс/Стоп", и выходом                       =====
              ; ==== С УСТАНОВКОЙ ПЕРЕМЕННОЙ RES В 0, в случае, если предварительно была            =====
             ; ==== нажата кнопка S2("Сброс/Стоп").                                                ====
            ; ==== Задержка от (1х0,1)=0,1 сек. до (255х0,1)=25,5 сек.       =========================
           ; ==== Входной параметр: в <R7> - кратен (целое число секунд задержки 1...255 X0,1 сек.)==
          ;-----------------------------------------------------------------------------------------
delay_n_x_01_sek_ESC:
                CLR	OE_BTN1	 ; ВКЛЮЧИМ БУФЕР КНОПОК
				
				lcall   delay_01_sek_ESC
                djnz    R7,delay_n_x_01_sek_ESC
                ;------------------------------------------------------------------------------
                
				SETB	OE_BTN1	 ;ВЫКЛЮЧИМ БУФЕР КНОПОК
EXIT_delay_n_x_01_sek_ESC_WITH_RES:				
				ret


